; ============================================================================
;        __
;   \\__/ o\    (C) 2020-2021  Robert Finch, Waterloo
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

	code
	align	4

;------------------------------------------------------------------------------
; Parameters:
;   none
; Returns:
;   $a0 = app id (0 to 31), -1 if none available
;------------------------------------------------------------------------------
  align 16
AllocAppId:
  ldt   $a1,APPID_POOL
  ldi   $a0,#31
.0002:
  bltz  $a1,.0001
  ldi   $t0,#1
  sll   $t0,$t0,$a0
  or    $a1,$a1,$t0
  stt   $a1,APPID_POOL
  ret
.0001:
  sll   $a1,$a1,#1
  sub   $a0,$a0,#1
  bge   $a0,$x0,.0002
  ldi   $a0,#-1
  ret

;------------------------------------------------------------------------------
; Parameters:
;   $a1 = pointer to application start record
; Modifies:
;		$s1 = pointer to application start record
;		$s3 = pointer to ACB
;------------------------------------------------------------------------------
  align 16
FMTK_StartApp:
IStartApp:
	sub		$sp,$sp,#4
	stt		$ra,[$sp]
  mov   $s1,$a1
  mov   $a0,$x0           ; OS owns the ACB
  call	AllocAppId
  bltz	$a0,.noMoreApps
  sll		$s2,$a0,#2
  ldi   $a0,#4096         ; size of application control block
  call  PAMAlloc          ; allocate ACB
  bltz	$a0,.noMem
  stt		$a0,ACBPtrs[$s2]	; store pointer to ACB in list
  mov   $s3,$a0           ; $s3 = pointer to ACB
  srl		$s2,$s2,#2
  stb   $s2,ACBmid[$s3]
  ldi   $t0,#ACB_MAGIC
  stt   $t0,OBJMagic[$s3]
  stt   $x0,ACBGarbageList[$s3]
  stt   $x0,ACBiofNext[$s3]
  stt   $x0,ACBiofPrev[$s3]
;  ldb   $t0,ASRmid[$s1]
  ldbu	$t0,ASRRegset[$s1]
  ; Allocate a virtual screen and set pointers to it.
;  srl   $a0,$s3,#LOG_PGSZ ; App owns the screen
  ldi   $a0,#8192         ; size of virtual screen
  call  PAMAlloc
  bltz  $a0,.noMem
  stt   $a0,ACBpVidMem[$s3]
  stt   $a0,ACBpVirtVidMem[$s3]
  ; Setup screen attributes
  ldi   $t0,#56
  stb   $t0,ACBVideoCols[$s3]
  ldi   $t0,#31
  stb   $t0,ACBVideoRows[$s3]
  stb   $x0,ACBCursorRow[$s3]
  stb   $x0,ACBCursorCol[$s3]
  ldi   $t0,#$0087FC00
  stt   $t0,ACBNormAttr[$s3]

  ; Allocate memory for application code and copy code into position
  ldbu	$a0,ACBmid[$s3]
  ldt   $a1,ASRCodeSize[$s1]
  beqz  $a1,.0004         ; 0 size code = in ROM
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBpCode[$s3] ; save pointer to code area in ACB
  mov   $s4,$a1           ; $s4 = pointer to application code
  ldt   $t0,ASRCodeSize[$s1]  ; $t0 = code size
  ldt   $s5,ASRpCode[$s1] ; $s5 = pointer to application code
.0001:
  ldt   $t1,[$s5]
  stt   $t1,[$s4]
  add   $s5,$s5,#4
  add   $s4,$s4,#4
  sub   $t0,$t0,#4
  bgtz  $t0,.0001
  bra   .0003
.0004:
  ldt   $s5,ASRpCode[$s1] ; $s5 = pointer to application code
  stt   $s5,ACBpCode[$s3] ; save pointer to code area in ACB

.0003:
  ; Allocate memory for application data and copy data into position
  ; The memory requirements for both initialized and uninitialized data are
  ; summed and a single block of memory allocated for both.
  ; Vars will then be accessed via a global pointer offset from the start
  ; of the data area.
  stt   $x0,ACBpData[$s3]
  stt   $x0,ACBpUIData[$s3]
  ldbu	$a0,ACBmid[$s3]
  ldt   $a1,ASRDataSize[$s1]
  ldt   $a2,ASRUIDataSize[$s1]
  add   $a1,$a1,$a2
  beqz  $a1,.0005         ; no data?
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBpData[$s3]

  ; Copy pre-initialized data to data area.
  mov   $s4,$a1           ; $s4 = pointer to application data
  ldt   $t0,ASRDataSize[$s1]  ; $t0 = data size
  add   $t0,$t0,#3        ; round to whole words
  and   $t0,$t0,#$FFFFFFFC
  ldt   $s5,ASRpData[$s1] ; $s5 = pointer to application data
.0002:
  ldt   $t1,[$s5]
  stt   $t1,[$s4]
  add   $s5,$s5,#4
  add   $s4,$s4,#4
  sub   $t0,$t0,#4
  bgtz  $t0,.0002

.0005:
  ; Record pointer to uninitialized data
  ldt   $a0,ASRDataSize[$s1]
  ldt   $a1,ACBpData[$s1]
  add   $a1,$a0,$a1
  add   $a1,$a1,#3
  and   $a1,$a1,#$FFFFFFFC
  stt   $a1,ACBpUIData[$s3]

.0006:
  ; Allocate memory for heap
  ldbu	$a0,ACBmid[$s3]
  ldt   $a1,ASRHeapSize[$s1]
  beqz  $a1,.0007         ; no heap?
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBHeap[$s3]

.0007:
  ldwu	$a1,ACBmid[$s3]		; $a1 = app id
  ldi   $a2,#1024         ; task local memory
  ldt   $a3,ASRpCode[$s1] ; get pointer to code area
  ldbu	$a4,ASRRegset[$s1]
  ldi		$t0,#52						; register set 52 and above are Machine
  blt		$a4,$t0,.norm
  or		$a4,$a4,#$300			; set Machine Mode
.norm:
  call  IStartTask        ; start the initial app task
  bnez  $a0,.tskErr       ; error?
  stw   $a1,ACBTask[$s3]
  sll   $t0,$a1,#2
  ldt		$t0,TCBPtrs[$t0]
  ldt   $t1,ACBpData[$s3]
  stt   $t1,TCBxGP[$t0]   ; set global pointer to data area
  ldi   $t1,#AS_LIVE
  stb   $t1,ACBStatus[$s3]
  ldwu	$a1,ACBmid[$s3]		; return app id
  ldi   $a0,#E_Ok
.retx:
  ldt		$ra,[$sp]
  add		$sp,$sp,#4
  ret
.tskErr:
  ldi   $t1,#AS_UNDEAD
  stb   $t1,ACBStatus[$s3]
  bra		.retx

.noMoreApps:
  ldi   $a0,#E_NoMoreAppIds
  bra		.retx
.noMem
	ldi		$a0,#E_NoMem
  bra		.retx

;------------------------------------------------------------------------------
; Exit the current task.
;
; Parameters:
;		none
; Modifies:
;		a1 = task id
;------------------------------------------------------------------------------
	align	16
FMTK_ExitApp:
	mGetCurrentTid
;	sll		$a1,$a0,#LOG_TCBSZ  ; convert TID to TCB address
	ldb   $a1,TCBmid[$a1]     ; get the mid
	bra		FMTK_KillApp
	
;------------------------------------------------------------------------------
; Parameters:
;		a1 = mid of task to kill
;------------------------------------------------------------------------------
  align 16
FMTK_KillApp:
	beqz	$a1,.immortal		    ; app #0 is immortal (the system)
	ldi		$t0,#TS_DEAD				; flag app as dead (prevents it from being re-queued)
	sll		$a1,$a1,#2	        
;	ldt   $a1,TidTcbMap[$a1]  ; convert TID to TCB address
	ldt		$a1,ACBTask[$a1]
	stb		$t0,TCBStatus[$a1]
;	ldbu  $a0,TCBmid[$a1]
;	call	FreeAll							; free all the memory associated with the task
	; Now make task ID available for reuse
	ldbu  $a0,TCBtid[$a1]
.immortal:
	ldi		$a0,#E_Ok
	ret

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

PickACB:
	ldi		$t0,#31*4
.0001:
	ldt		$a0,ACBPtrs[$t0]
	and		$a0,$a0,#$07FC00
	bnez	$a0,.xit
	sub		$t0,$t0,#4
	bge		$t0,$x0,.0001
.xit:
	ret

DumpACBPtrs:
	sub		$sp,$sp,#8
	stt		$ra,[$sp]
	ldi		$t0,#0
	stt		$t0,4[$sp]
	ldi		$a0,#msgACBPtrs
	call	PutString
	ldt		$t0,4[$sp]
.0001:
	stt		$t0,4[$sp]
	ldt		$a0,ACBPtrs[$t0]
	call	PutHexWord
	ldi		$a0,#CR
	call	Putch
	ldt		$t0,4[$sp]
	add		$t0,$t0,#4
	ldi		$t1,#128
	blt		$t0,$t1,.0001
	ldt		$ra,[$sp]
	add		$sp,$sp,#8
	ret

DumpACB:
	ret

msgACBPtrs:
	db		"ACB Pointers",CR,LNFD,0
