; ============================================================================
;        __
;   \\__/ o\    (C) 2020-2021  Robert Finch, Waterloo
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

	code

;-----------------------------------------------------------------------------
; Determine if the currently running app has the I/O focus.
;
; Stack Space:
;		none
; Parameters:
;		none
; Modifies:
;		none
; Returns:
;		$a0 = E_Ok
;		$a1 = 1 if task has IO focus, 0 otherwise
;-----------------------------------------------------------------------------
	align	16
FMTK_HasIOFocus:
	mGetCurrentTid
	sll   $a0,$a0,#2
	ldt		$a0,TCBPtrs[$a0]
	and		$a0,$a0,#$FFC00
	ldwu  $a0,TCBappid[$a0]
	beqz  $a0,.hasFocus       ; startup appid = zero
	sll		$a0,$a0,#2
	ldt		$a0,ACBPtrs[$a0]		; $a0 = ACB pointer
	and		$a0,$a0,#$FFC00
	ldt   $a1,IOFocusNdx
	bne   $a1,$a0,.notEqual
.hasFocus:
	ldi   $a1,#1
	ldi		$a0,#E_Ok
	ret
.notEqual:
  ldi   $a1,#0
	ldi		$a0,#E_Ok
	ret

;-----------------------------------------------------------------------------
; First check if it's even possible to switch the focus to another
; app. The I/O focus list could be empty or there may be only a
; single task in the list. In either case it's not possible to
; switch.
;
;	Parameters:
;		none
;	Modifies:
;		none
;	Returns:
;		a0 = E_Ok
;-----------------------------------------------------------------------------

	align	16
SwitchIOFocusHelper:
	sub		$sp,$sp,#4
	stt		$ra,[$sp]
	; Ensure the focus index is valid
  ldt   $t0,IOFocusNdx
  ldt		$t1,OBJMagic[$t0]
  xor		$t1,$t1,#ACB_MAGIC
  bnez	$t1,.pickACB
  ldt   $t1,ACBiofNext[$t0]
  ; Ensure new choice is valid
  beqz  $t1,.zeroAppId
  ldt		$t2,OBJMagic[$t1]
  xor		$t2,$t2,#ACB_MAGIC
  bnez	$t2,.zeroAppId
  stt   $t1,IOFocusNdx
  bne   $t0,$t1,.notSame
.zeroAppId:
	ldi		$a0,E_Ok
	ldt		$ra,[$sp]
	add		$sp,$sp,#4
	ret
.notSame:
	; swap virtual screens
	; set vidmem pointer
	bra		.zeroAppId
.pickACB:
	call	PickACB
	stt		$a0,IOFocusNdx
	bra		.zeroAppId
    
	align	16
FMTK_SwitchIOFocus:
	jmp		SwitchIOFocusHelper

;-----------------------------------------------------------------------------
; RequestIOFocus requests that the task receive the I/O focus. It does not
; mean the task will receive the focus immediately. Instead it is placed
; on a list of requesters. Which task has the I/O focus is determined from
; user input.
; The I/O focus list is an array indicating which apps are requesting the
; I/O focus. The I/O focus is user controlled by pressing CNTRL-T on the
; keyboard.
;
; Parameters:
;		$a1 = app id requesting focus for
; Returns:
;   $a0 = E_Ok
;   $a1 (unmodified)
;-----------------------------------------------------------------------------

	align	16
FMTK_RequestIOFocus:
	ldi		$a2,#31
	bgeu	$a1,$a2,.badAppId
	sub		$sp,$sp,#4
	stt		$ra,[$sp]
	ldi   $a2,#1
	sll		$t2,$a1,#2
	ldt		$t2,ACBPtrs[$t2]
	and		$t2,$t2,#$FFC00
	beqz	$t2,.badAppId
	call	LockOS
	ldb		$t1,ACBHasFocus[$t2]  ; Is the task already included in the IO focus?
	bnez	$t1,.ret				      ; If so, don't add again
	ldt	  $t0,IOFocusNdx			  ; get current index
	bnez	$t0,.notEmpty		      ; is there one? (!= 0)
	stt   $t2,IOFocusNdx
	stt   $t2,ACBiofNext[$t2]
	stt   $t2,ACBiofPrev[$t2]
	jmp   .ret
.notEmpty:
  ; Insert just before head which is end of queue
  ldt   $t1,ACBiofPrev[$t0]
  stt   $t2,ACBiofPrev[$t0]     ; head->prev = new
  stt   $t2,ACBiofNext[$t1]     ; head->prev->next = new
  stt   $t0,ACBiofNext[$t2]     ; new->next = head
  stt   $t1,ACBiofPrev[$t2]     ; new->prev = head->prev
.ret:
	stb   $a2,ACBHasFocus[$t2]  ; set has focus flag
	call	UnlockOS
	ldt		$ra,[$sp]
	add		$sp,$sp,#4
	ldi		$a0,#E_Ok
	ret
.badAppId:
	ldi		$a0,#E_BadAppId
	ret

;-----------------------------------------------------------------------------
; ReleaseIOFocus called when the app no longer desires to be on the I/O
; focus list.
;-----------------------------------------------------------------------------

	align	16
FMTK_ReleaseIOFocus:
	mGetCurrentTid
	sll   $a1,$a0,#LOG_TCBSZ
	ldwu  $a1,TCBappid[$a1]
	jmp		FMTK_ForceReleaseIOFocus

;-----------------------------------------------------------------------------
; Releasing the I/O focus causes the focus to switch if the running app
; had the I/O focus.
; ForceReleaseIOFocus forces the release of the IO focus for a app
; different than the one currently running.
; 
; Stack Space:
;		2 words
; Parameters:
;		$a1 = app id to release
; Returns:
;		$a0 = E_Ok
;-----------------------------------------------------------------------------

	align	16
FMTK_ForceReleaseIOFocus:
	sub		$sp,$sp,#4
	stt		$ra,[$sp]
	call	LockOS
	sll		$t0,$a1,#2
	ldt		$t0,ACBPtrs[$t0]
	and		$t0,$t0,#$FFC00
	ldb		$t1,ACBHasFocus[$t0]
	beqz	$t1,.noFocus	    ; does it even have the focus?
	stb   $x0,ACBHasFocus[$t0]
	ldt   $t1,IOFocusNdx
	bne   $t0,$t1,.notAtTop
	ldt   $t1,ACBiofNext[$t0]
	bne   $t1,$t0,.0001
	; removing last entry
	stt   $x0,IOFocusNdx
	stt   $x0,ACBiofNext[$t0]
	stt   $x0,ACBiofPrev[$t0]
	bra		.noFocus
.0001:	
	stt   $t1,IOFocusNdx
.notAtTop:
	ldt   $t1,ACBiofNext[$t0]
	ldt   $t2,ACBiofPrev[$t0]
	stt   $t2,ACBiofPrev[$t1]
	stt   $t1,ACBiofNext[$t2]
	stt   $x0,ACBiofNext[$t0]
	stt   $x0,ACBiofPrev[$t0]
.noFocus:
	call	UnlockOS
	ldt		$ra,[$sp]
	add		$sp,$sp,#4
	ldi		$a0,#E_Ok
	ret

;------------------------------------------------------------------------------
;     Wait for the IO Focus. Returns only when focus is present. This function
; calls PeekChar() to see if there's a CTRL-T in the buffer which means to
; switch the I/O focus.
;
; Parameters:
;   a1 = device # to check, 5 = COM1 serial port, 1 = KBD keyboard
;------------------------------------------------------------------------------

	align	16
_WaitForIOFocus:
WaitForIOFocus:
  sub 	$sp,$sp,#12
  stt   $a0,[$sp]
  stt   $a1,4[$sp]
  stt   $a2,8[$sp]
.WFF1:
	ldi		$a0,#20           ; FMTK Has Focus
	ecall
	bnez	$a1,.HasFocus
	ldi   $a0,#26           ; FMTK_IO
	ldi   $a1,#5            ; hardcoded to serial port for now
	ldi   $a2,#9            ; peekchar function
	ecall
;	call	SerialPeekChar
	bltz  $a1,.0001
.0001:
	ldi		$a0,#$14					; CTRL-T
	bne		$a1,$a0,.WFF2
;	call	SerialGetChar
	; eat up the CTRL-T
	ldi		$a0,#26						; FMTK_IO
	ldi   $a1,#5  ;4[$sp]        ; get back device #
	ldi		$a2,#8						; getchar function
	ecall
	ldi		$a0,#21						; switch IO Focus
	ecall
	bra		.WFF1
.WFF2:
	ldi		$a0,#5            ; FMTK Sleep() function
	ldi		$a1,#1
	ecall
	bra		.WFF1
.HasFocus:
  ldt   $a0,[$sp]
  ldt   $a1,4[$sp]
  ldt   $a2,8[$sp]
  add   $sp,$sp,#12
  ret

;------------------------------------------------------------------------------

	align	16
GetUIFlag:
  sub 	$sp,$sp,#12
  stt		$ra,[$sp]
  stt   $a2,4[$sp]
  stt   $a3,8[$sp]
  ldi   $a0,#27           ; Get DCB Field
  ldi   $a2,#DCB_fUI      ; Get UI Flag
  ldi   $a3,#1            ; 1 byte
  ecall
  ldt		$ra,[$sp]
  ldt   $a2,4[$sp]
  ldt   $a3,8[$sp]
  add   $sp,$sp,#12
  ret
