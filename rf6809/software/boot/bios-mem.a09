; ============================================================================
;        __
;   \\__/ o\    (C) 2020-2022  Robert Finch, Waterloo
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; BSD 3-Clause License
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions are met:
;
; 1. Redistributions of source code must retain the above copyright notice, this
;    list of conditions and the following disclaimer.
;
; 2. Redistributions in binary form must reproduce the above copyright notice,
;    this list of conditions and the following disclaimer in the documentation
;    and/or other materials provided with the distribution.
;
; 3. Neither the name of the copyright holder nor the names of its
;    contributors may be used to endorse or promote products derived from
;    this software without specific prior written permission.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
; DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
; FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
; DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
; SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
; CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
; OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
; OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;
; ============================================================================

; The max page that will be used for an app. Pages above this are for the
; OS ROMs. 

;------------------------------------------------------------------------------
; Page Mapping Ram centric MMU
;------------------------------------------------------------------------------

MMUInit:
	ldd		#MEMSZ-OSPAGES 	; set number of available pages (20 pages already allocated)
	std		NPAGES
	; Free all memory for all tasks
	; Sets all pages to map to page zero for all tasks, except for the system
	; task (task #0) which pre-allocates the first 16kB of memory.
	clrd
	tfr		d,y	
MMUInit2:
	stb		MMU_AKEY				; set access key
	ldx		#0
MMUInit1:
	sty		MMU,x
	leax	2,x
	cmpx	#MMU_ENTRIES
	blo		MMUInit1
	incb
	cmpb	#32
	blo		MMUInit2

	; allocate last page for system stacks
	clr		MMU_AKEY				; select Mid #0 (system)
	ldd		#$FFFE0F				; read/write/cacheable page $FFFF (last page of RAM)
	ldx		#$DFE
	std		MMU,x

	ldb		#$C00
	ldy		#MidStackBottoms
0001:
	stb		,y
	iny
	cmpy	#MidStackBottoms+32
	blo		0001b

;------------------------------------------------------------------------------
; Set MAP #0 up as a linear memory map
; - semaphore memory is mapped
; - ROMs are mapped
; - The text screen memory is mapped.
;------------------------------------------------------------------------------

	clr		MMU_AKEY
	ldx		#0
IFD &CMODA709
	ldb		#$E00			; RWX=111
ELSE
	ldd		#$F00000	; CRWX=1111
ENDIF
	clre
0001:
	stb		MMU,x
IFD &CMODA709
	inx
ELSE
	sta		MMU+1,x
	leax	2,x
ENDIF
	incb
	ince
IFD &COMDA709
	cmpe	#1
ELSE
	cmpe	#1792			; number of entries to update
ENDIF
	blo		0001b
	; Map the MMU table into the address space.
	; (requires only 1 page)
;	ldx		#$E38			; E38000 = page 71C * 2 bytes per record
;	ldd		#$F1C6FF	; high order bits are all ones, plus CRWX=0110
;	std		MMU,x
	; Map semaphore memory into address space.
;	ldx		#$EF0
;	ldd		#SEMA_MMU
;	std		MMU,x
	; Map LEDs (VIA) into address space
;	ldx		#$E60
;	ldd		#VIA_MMU
;	std		MMU,x
	; Map ROMs into address space
	bsr		MapROM

	; Map Text Screen
	; Map Entire I/O region
	bsr		MapIO

	ldb		#$800
	stb		MMU_OKEY	; turn on mapping
	rts

; Map ROMs into the current address space
; ROM is mapped in the 1MB region $F00000 to $FFFFFF

IFD &CMODA709
MapROM:
	ldx		#$F8
	lda		#$FF8
	clre
0001:
	sta		MMU,x
	inx
	inca
	ince
	cmpe	#8
	blo		0001b
	rts
ELSE
MapROM:
	ldx		#$F00
	lda		#$F80
	ldb		#$FFF			; F=cachable, readable, writeable executable
	clre
0001:
	sta		MMU,x
	stb		MMU+1,x
	leax	2,x
	inca
	ince
	cmpe	#120
	blo		0002f
	ldb		#$DFF			; make hi rom read-only
0002:
	cmpe	#128
	blo		0001b
	rts
ENDIF

; Map I/O into the current address space
; I/O is mapped in the 1MB region $E00000 to $EFFFFF

IFD &CMODA709
MapIO:
	ldx		#$E00
	lda		#$FF0
	clre						; e = 0
0001:
	sta		MMU,x
	inx
	inca
	ince
	cmpe	#8
	blo		0001b
	rts
ELSE
MapIO:
	ldx		#$E00
	lda		#$F00
	ldb		#$6FF			; 6 = readable, writable
	clre						; e = 0
0001:
	sta		MMU,x
	stb		MMU+1,x
	leax	2,x
	inca
	ince
	cmpe	#128
	blo		0001b
	rts
ENDIF

;------------------------------------------------------------------------------
; Find a run of buckets available for mapping virtual to physical addresses.
;
; Parameters:
;		D = mid
;		X = number of pages required.
; Modifies:
;		
; Returns:
;		D = starting bucket number, -1 if no run found
;   N flag set if no run found
;------------------------------------------------------------------------------

FindRun:
	pshs		u,w
	cmpx		#0								; make sure a reasonable request is made
	beq			FindRun0002
	cmpx		#MAXVPG
	bhs			FindRun0002
	stb			MMU_AKEY					; set MMU access key
	tfr			x,y								; Y = number of consecutive empty buckets needed
	ldd			#OSPAGES					; start looking at page D=8 (others are for OS)
IFND &CMODA709
	asld
ENDIF
	tfr			d,u
	; First find an empty bucket
FindRun0001:
IFD &COMDA709
	ldb			MMU,u
ELSE
	ldd			MMU,u							; get map entry into U
ENDIF
	beq			FindRunEmpty0     ; is it empty?
IFD &CMODA709
	leau		1,u
	cmpu		#MAXVPG
ELSE
	leau		2,u
	cmpu		#MAXVPG*2
ENDIF
	blo			FindRun0001
FindRun0002:
	ldd			#-1								; got here so no run was found
	puls		u,w,pc
	; Check subsequent buckets for emptiness
FindRunEmpty0:
	tfr			u,w								; w = start of run bucket
FindRunEmpty1:
	ldd			MMU,u							; get bucket value
	bne			FindRun0003
	dey
	beq		  FindRunFoundEnough
	leau		2,u								; advance to next bucket
	bra			FindRunEmpty1
FindRun0003:
	leau		2,u								; increment to next bucket
	tfr			x,y								; reset required count
	bra			FindRun0001				; continue search
FindRunFoundEnough:
	tfr			w,d
	puls		u,w,pc

;------------------------------------------------------------------------------
; AllocMem allocates memory in the given memory map. The allocated memory is
; zeroed out.
;
; Parameters:
;		D = mid
;		X = amount of memory to allocate
; Modifies:
;		t0
; Returns:
;		X = pointer to allocated memory in virtual address space.
;		D = E_Ok for success, E_NotAlloc otherwise
;   Z flag set if successful
;------------------------------------------------------------------------------
;
AllocMem:
	andcc		#$D6F							; enable interrupts
	pshs		w,y,u
	stb			MMU_AKEY
	tfr			x,d
	tfr			x,w								; w = size of allocation
	beq     allocZero
	; First check if there are enough pages available in the system.
	addd		#32								; word
	addd		#PGSZ-1						; D = round memory request
	tfr			a,b								; convert to pages required (/8192)
	clra
	lsrb
	cmpb		NPAGES						; check number of pages available
	bls		  allocEnough
allocNoRun2:
	ldx			#0								; not enough, return null
	bra			allocNoRun
allocEnough:
	tfr			d,u								; u = number of pages
	; There are enough pages, but is there a run long enough in map space?
	pshs		d									; save required # pages
	bsr 		FindRun						; find a run of available slots
	tsta
	puls		x									; X = number of pages required
	bmi			allocNoRun
	; Now there are enough pages, and a run available, so allocate
	pshs		d									; save start of run
	ldd			NPAGES						; decrease number of pages available in system
	subr		x,d
	std			NPAGES
	puls		d									; D = start of run
	pshs		d
alloc0001:
	pshs		d									; save current run page
	tfr			d,y								; convert D to index
IFND &CMODA709
	addr		y,y
ENDIF
	swi
	fcb			MF_PAMAlloc					; alloc page
IFD &CMODA709
	ora			#$E00							; set ACR: cacheable, readable, writeable, executable
	stb			MMU,y							; store translation in table
ELSE
	ora			#$F00							; set ACR: cacheable, readable, writeable, executable
	stb			MMU,y							; store translation in table
	sta			MMU+1,y
ENDIF
	ldd			,s								; D = page number within run
IFD &CMODA709
	tfr			b,a								; * 4096
	clrb
	lsra											; * 2048
	rorb
ELSE
	tfr			b,a								; * 4096
	clrb
	asla											; * 8192 (D now equal pointer to mem)
ENDIF
	tfr			d,y								; Y = pointer to mem
	clrd											; value to set memory to
	clre											; 4096 words to clear
alloc0004:
IFD &CMODA709
	stb			,y
	iny												; index to next word
ELSE
	std			,y
	leay		2,y								; index to next word
ENDIF
	dece											; decrement word count
	bne			alloc0004					; until zero again
	puls		d									; D = page number in run
  incd											; D = next page number in run
  dex												; decrement number in run
  bne			alloc0001
	puls		d									; D = start of run
IFD &CMODA709
	tfr			b,a								; * 4096
	clrb											; 
	lsra											; * 2048
	rorb
ELSE
	tfr			b,a								; * 4096
	clrb
	asla											; * 8192 (D now equal pointer to mem)
ENDIF
	; Setup object fields
	tfr			d,x								; X = pointer to mem
	exg			d,u								; D = numm pages
IFD &CMODA709
	tfr			b,a								; num pages * 2048
	clrb
	lsra											; *2048
	rorb
ELSE
	tfr			b,a								; num pages * 8192
	clrb
	asla
ENDIF
	std			OBJ_SIZE,x
	ldd			#('M'<<16)+('E'<<8)+'M'
	std			OBJMagic,x
	leax		32,x							; point past object header
	ldd			#E_Ok
	puls		w,y,u,pc
allocNoRun:
allocZero:
	ldd			#E_NotAlloc
	puls		w,y,u,pc

;------------------------------------------------------------------------------
; Allocate the stack page for a task. The stack is located at $FDFFFF and
; downwards. The virtual address of the stack is fixed at $FDE000. The physical
; page varies.
;
; Parameters:
;		b = mid to allocate for
;	Returns:
;		d = physical address, 0 if unsuccessful
;		x = virtual address, not valid unless successful
;------------------------------------------------------------------------------

AllocStack:
	pshs		ccr
	stb			MMU_AKEY				; set access key for table
IFD &CMODA709
	ldx			#MAXVPG
ELSE
	ldx			#MAXVPG*2				; last page of memory is for stack
ENDIF
	orcc		#$290						; mask off interrupts
IFD &CMODA709
	ldb			MMU,x
ELSE
	ldd			MMU,x						; check if stack already allocated
ENDIF
	bne		  asAlreadyAlloc
	swi
	fcb    	MF_PAMAlloc				; allocate a page
	tstd
	beq		  astkXit    			; success?
IFD &CMODA709
	orb			#$C00						; set page for read / write access
	ldx			#MAXVPG					; last page of memory is for stack
	stb			MMU,x
ELSE
	exg			a,b
	orb			#$E00						; set page for cacheable read / write access
	ldx			#MAXVPG*2				; last page of memory is for stack
	std			MMU,x
ENDIF
asAlreadyAlloc:
IFD &CMODA709
	tfr			b,a
	clrb
	lsra
	rorb
ELSE
	clra										; convert pages to addresses (*8192)
	aslb										; B already has high-order byte, so is *4096
ENDIF
	ldx			#MAXVPG*PGSZ
astkXit:
	puls		ccr,pc

;------------------------------------------------------------------------------
; Increase the stack allocation
;
; Parameter:
;		B = mid to allocate for
; Returns:
;		D = page number of bottom of stack
;------------------------------------------------------------------------------

IncreaseStackSize:
	stb		MMU_AKEY
	ldy		#MidStackBottoms
	ldb		b,y
	clra
	decb										; down a page
	tfr		b,e								; E = new stack bottom
IFND &CMODA709
	aslb
ENDIF
	tfr		d,x
IFD &CMODA709
	ldb		MMU,x
ELSE
	ldd		MMU,x							; check if virtual address available
ENDIF
	bne		IncreaseStackOutOfMem	; crashed into other allocation?
	swi
	fcb		MF_PAMAlloc					; allocate page D = page number
	tstd
	bmi		IncreaseStackOutOfMem
IFD &CMODA709
	orb		#$E00
	stb		MMU,x
ELSE
	ora		#$F00							; ACR = CRWX
	exg		a,b
	std		MMU,x
	exg		a,b
	anda	#$0FF
ENDIF
	ldf		MMU_AKEY
	ste		f,y
	rts
IncreaseStackOutOfMem:
	clrd
	rts
	
;------------------------------------------------------------------------------
; This routine will de-allocate all the pages associated with a map including
; the stack.
;
; Parameters:
;		D = mid to free memory for
;	Modifies:
;		
; Returns:
;		none
;------------------------------------------------------------------------------

FreeAll:
	pshs		u,ccr
	tstb										; cannot freeall system map
	beq			FreeAllXit
	stb			MMU_AKEY				; set access key to MMU
	ldu			#-1							; start at first entry
FreeAllNxt:
	andcc		#$D6F						; enable interrupts
	leau		1,u							; increment page number
	cmpu		#MAXVPG+1				; number of buckets to check, dont free OS memory
	bhs			FreeAllXit
	tfr			u,d							; D = page number
IFND &CMODA709
	aslb										; convert page number to MMU table index
ENDIF
	tfr			d,x
	orcc		#$290						; mask off interrupts
IFD &CMODA709
	ldb			MMU,x						; get current mapping
	andb		#$1FF						; mask off ACR
	clra
ELSE
	ldd			MMU,x						; get current mapping
	andb		#$0FF						; mask off ACR
	exg			a,b							; D = page number
ENDIF
	tfr			d,x
	tst			PAMShareCounts,x	; check the share count for the page
	beq			FreeAll1				
	dec			PAMShareCounts,x	; decrement share count if non-zero
	bne			FreeAllNxt			; we're done with this page if share count non-zero
FreeAll1:
	swi
	fcb			MF_PAMFree
IFND &CMODA709
	aslb										; D = index into MMU tables
ENDIF
	tfr			d,x							; X = index into MMU tables
	clr			MMU,x						; clear the entry in the MMU (marks as free)
IFND &CMODA709
	clr			MMU+1,x
ENDIF
	inc			NPAGES					; update the number of available pages
	bra			FreeAllNxt
FreeAllXit:
	puls		u,ccr,pc


;------------------------------------------------------------------------------
; Convert a virtual address to a physical one. The physical address may contain
; more than 24 bits so it is returned in the D and X registers.
;
; Stack Space:
;		1 word
; Parameters:
;		B = mid
;		X = virtual address to convert
; Returns:
;		D,X = physical address
;------------------------------------------------------------------------------

VirtToPhys:
	pshs	w
	exg		x,d
IFD &CMODA709
	aslb
	bcc		0001f
	tfr		a,b
	lsrb
	orb		#$800
	bra		0002f
0001:
	tfr		a,b
	lsrb
0002:
ELSE
	tfr		a,b									; convert virtual address to page number (/8192)
	clra											; (divide by 8192 then multiply by 2 = /4096)
	andb	#$FFE								; mask for table index
ENDIF
	exg		d,x
	stb		MMU_AKEY						; set access key for table
IFD &CMODA709
	ldb		MMU,x
	andb	#$1FF								; mask off ACR bits
	exg		a,b
ELSE
	ldd		MMU,x								; get the translation
	andb	#$0FF								; mask off ACR bits
ENDIF
	muld	#PGSZ
	tfr		w,x
	puls	w,pc
